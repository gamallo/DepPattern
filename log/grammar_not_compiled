##########GRAMATICA COMUM PARA PT, ES, GL, FR################################

#Declarar verbos como nao nominativos por defeito:
Single: VERB
Add: nomin:no
%


##corrections

########First corrections tree-tagger
Single: PRP<lemma:pero|because|though|if|whether|while> [X]?
Corr: tag:CONJ, type:S
%


## o 'que' apos nome e pronome relativo
Single: [NOUN] [Fc]? CONJ<lemma:que> [NOUN] [VERB]
Corr: tag:PRO, type:R
%
#
# o 'que' apos determinante 'lo que')
Single: [X<lemma:lo>] CONJ<lemma:que>
Corr: tag:PRO, type:R
%
Single: X<lemma:lo> [PRO<lemma:que>]
Corr: tag:DT, type:A
%

## a conjunçao 'pero' prefior trata-la como subordinante
Single: [X]? CONJ<lemma:pero>
Corr: type:S
%

###Nomes de tempo que são advérbios: (#re-corrigir em nome se vai antes de derminante!)
Single: [X]? NOUN<lemma:$AdvTemp>
Corr: tag:ADV
%

##'creo' sempre aparece como verbo crear
Single: VERB<token:[cC]reo> [X]? [X<lemma:que>]
Corr: lemma:creer
%


##########tirar aspas de uma palavra#############

PunctR: X Fz|Fe
% 
PunctL: Fz|Fe X
%

##########FIM tirar aspas#############


##########LEXICAL RULES 
>: VERB<lemma:tener|ter|haber|haver|take|have> NOUN<number:S> [PRP]
%
>: VERB<lemma:be|ser> ADJ [PRP]
%

<: [VERB<lemma:ser|tornar|converter|be|become>] ADV<lemma:$Quant> ADJ [CONJ|PRO<lemma:que|como>]
NEXT
>: VERB<lemma:ser|tornar|converter|be|become> [ADV<lemma:$Quant>] ADJ [CONJ|PRO<lemma:que|como>]
NEXT
>: VERB<lemma:ser|tornar|converter|be|become>  [ADV<lemma:$Quant>] [ADJ] CONJ|PRO<lemma:que|como>
%

##falta transformarse/convertirse en menos popular que...



#####################FRASE ADVERBIAL E ADJECTIVAL####################


########coordinaçao ADJ 
CoordL: ADV [Fc] [ADV] CONJ<(type:C)|(lemma:$CCord)> [ADV]
NEXT
PunctL: [ADV] Fc [ADV] CONJ<(type:C)|(lemma:$CCord)> [ADV]
Recursivity: 10
%

CoordL: [Fc]? ADV CONJ<(type:C)|(lemma:$CCord)> [ADV]
NEXT
CoordR: [Fc]? [ADV] CONJ<(type:C)|(lemma:$CCord)> ADV
Add: coord:adv
%

PunctL: Fc CONJ<coord:adv>
%
############################

AdjnL: ADV<lemma:$Quant> ADV|ADJ
%

AdjnL: ADV<lemma:[Mm]ais\@de> [DET]? CARD|NOUN
%

###dous termos sem AND:
PunctL: [ADJ] Fc ADJ [NOUN]
NEXT
AdjnL: ADJ [Fc] ADJ [NOUN]
Recursivity: 5
%


########coordinaçao ADJ 
CoordL: ADJ [Fc] [ADJ] CONJ<(type:C)|(lemma:$CCord)> [ADJ]
NEXT
PunctL: [ADJ] Fc [ADJ] CONJ<(type:C)|(lemma:$CCord)> [ADJ]
Recursivity: 10
%

CoordL: [Fc]? ADJ CONJ<(type:C)|(lemma:$CCord)> [ADJ]
NEXT
CoordR: [Fc]? [ADJ] CONJ<(type:C)|(lemma:$CCord)> ADJ
Add: coord:adj
Inherit: gender, number
%

PunctL: Fc CONJ<coord:adj>
%
##tratar "carne buena y mal pescado"


#####################FIM FRASE ADVERBIAL E ADJECTIVAL################


##antes de tratar o NOUN+ADJ como Adjn, hai que tratar os predicativos de objecto directo (problemas: "tengo la pierna cansada" ; "tengo un coche rojo" 
####AtrR: VERB<lemma:$SubcatODirAtr>  [DET]? [NOUN] ADJ|CONJ<coord:adj>|VERB<mode:P>
###%


#################FRASE SUSTANTIVA#####################################
AdjnL: ADJ|CONJ<coord:adj>|CARD [ADV]? NOUN
Agreement: gender, number
Recursivity: 1
%


AdjnR: NOUN [ADV]? ADJ|CONJ<coord:adj>
Agreement: gender, number
Recursivity: 1
%

PunctR: NOUN Fc [ADJ|CONJ<coord:adj>]
NEXT
AdjnR: NOUN [Fc] ADJ|CONJ<coord:adj>
Agreement: gender, number
%


AdjnR:  NOUN NOUN
Recursivity: 1
%

#################FIM FRASE SUSTANTIVA########################




#################FRASE NOMINAL########################

##corrections

Single: [DET] ADJ [PRP]
Corr: tag:NOUN
%


##as simply the man
#AdjnL: [PRP] ADV [DET]  NOUN
#%


#o seu NOUN - todos os NOUN
SpecL: DET DET|PRO<type:X> [NOUN]
NEXT
SpecL: [DET] DET|PRO<type:X> NOUN
%

#todo lo que / todo el que
SpecL: DET<type:I> DET<type:A> [PRO<type:R>]
%

AdjnL: [DET] VERB<mode:P> NOUN
NEXT
SpecL: DET [VERB<mode:P>] NOUN
%

SpecL: DET CARD|DATE
%

SpecL: DET NOUN
#Recursivity: 1
%

##el tuyo
SpecL: DET<type:A> PRO<type:X>
%

##lo que me gusta (sustantivas)
SpecL: DET<type:[AD]>|PRO<type:D> PRO<type:[RW]>
Add: sust:yes
Inherit: number, person
%

##el de Mario, uno de esos

SpecL: DET<type:[AD]>|PRO<type:D> PRP<lemma:de> [NOUNSINGLE|PRO<type:D|P|I|X>]
Add: nomin:yes
Inherit: number, person
%


##o comer
#SpecL: DET<type:A> VERB<mode:N>
#%

##A melhorar ...
#SpecL: PRO<lemma:what> NOUN
#%

#################FIM FRASE NOMINAL########################



#####################FRASE VERBAL####################

##########Cliticos

##identificar verbos reflexivos ou pronominais:

ClitL: PRO<lemma:se> VERB
Add: se:yes
Uniq
%
ClitR: VERB PRO<lemma:se>
Add: se:yes
Uniq
%
ClitL: PRO<token:$cliticoind> VERB
Add: ind:yes
Uniq
%
ClitR: VERB PRO<token:$cliticoind>
Add: ind:yes
Uniq
%


##eliminar os cliticos (nao se lhes da funçao especifica)
ClitL: PRO<token:$cliticopers> VERB
Recursivity: 1
%

ClitR: VERB PRO<token:$cliticopers>
Recursivity: 1
%

###########AUXILIARES
VSpecL: VERB<type:S> [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? VERB<mode:P> 
Add: voice:passive
Inherit: mode, person, tense, number
%


VSpecL: VERB<(type:A)|(lemma:ter|haver|haber|avoir|have)> [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? VERB<mode:P> 
Add: type:perfect
Inherit: mode, person, tense, number
%



##antes de tratar as perífrases:
## "parece cansado" (V+VPP), hai que tratar os predicativos de verbos atributivos, ja que hai muitos adjectivos etiquetados como participios
#AtrR: VERB<lemma:$SubcatAtr>  VERB<mode:P>
#%
##lo que me gusta es cantar
#AtrR: VERB<lemma:ser> VERB<mode:N>
#%

#######PERIFRASES
#verbos modais
VSpecL: VERB<lemma:$VModalES> [ADV]? VERB<mode:N>
#NoUniq
Inherit: mode, person, tense, number
%
##conj+que 'hay que, tienes que'
VSpecL: [VERB<lemma:tener|haber>] [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? CONJ<lemma:que&type:S> [ADV]? VERB<mode:N> 
NEXT
VSpecL: VERB<lemma:tener|haber> [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [CONJ<lemma:que&type:S>] [ADV]? VERB<mode:N> 
Inherit: mode, person, tense, number
%


##incoativos 'voy a'
VSpecL: [VERB<lemma:ir|venir|empezar|comenzar|acabar|finalizar|terminar|pasar|estar>] [ADV]? PRP<lemma:$PrepLocs> [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? VERB<mode:N> 
NEXT
VSpecL: VERB<lemma:ir|deber|venir|empezar|comenzar|acabar|finalizar|terminar|pasar|estar> [ADV]? [PRP<lemma:$PrepLocs>] [ADV]? VERB<mode:N>
Inherit: mode, person, tense, number
%


##outras modalidades (entre obj. direito e perífrase): suele comer, quiere comer...
VSpecL: VERB<lemma:querer|desear|pensar|soler|acostumbrar> [ADV]? VERB<mode:N>
#NoUniq
Inherit: mode, person, tense, number
%


##estoy comiendo
VSpecL: VERB<lemma:estar> [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? [ADV]? VERB<mode:G> 
Inherit: mode, person, tense, number
%


#########COMPLEMENTOS ADVERVIAIS

######adverbios entre virgulas

PunctL: [ADV<pos:0>] Fc VERB
NEXT
AdjnL: ADV<pos:0> [Fc] VERB
%


PunctR:  VERB [Fc]? [ADV] Fc
NEXT
PunctR: VERB Fc [ADV] [Fc]
NEXT
AdjnR: VERB [Fc]? ADV [Fc]
%

PunctL: Fc [ADV] [Fc]? VERB
NEXT
PunctL: [Fc] [ADV] Fc VERB
NEXT
AdjnL: [Fc] ADV [Fc]? VERB
%

AdjnR: VERB [NOUN|PRO<type:D|P|I|X>]? ADV|CONJ<coord:adv> 
Recursivity: 1
%

AdjnL:  ADV|CONJ<coord:adv>  VERB
Recursivity: 1
%


#####################FIM FRASE VERBAL####################




##################Frase Preposicionais de nomes#################


########coordinaçao compl prep
TermR: PRP NOUN [Fc] [PRP] [NOUN] [CONJ<lemma:$CCord>] [PRP] [NOUN]
NEXT
CoordL: PRP [NOUN] [Fc] [PRP] [NOUN] CONJ<lemma:$CCord> [PRP] [NOUN]
NEXT
PunctL: [PRP] [NOUN] Fc [PRP] [NOUN] CONJ<lemma:$CCord> [PRP] [NOUN]
Recursivity: 3
%

CoordL: [Fc]? PRP [NOUN] CONJ<lemma:$CCord> [PRP] [NOUN]
NEXT
TermR: [Fc]? [PRP] [NOUN] [CONJ<lemma:$CCord>] PRP NOUN
NEXT
TermR: [Fc]? PRP NOUN [CONJ<lemma:$CCord>] [PRP] [NOUN]
NEXT
CoordR: [Fc]? [PRP] [NOUN] CONJ<lemma:$CCord> PRP [NOUN]
Add: coord:cprep
%
####fim coordinaçao compl prep

###Right attachment (RA) em complementos nominais
###Primeiro com uma lista de preposiçoes RA, onde o antecdente tem que ser nome comum
CprepR: [NOUNSINGLE] [PRP] [NOUNSINGLE] [PRP] [NOUNSINGLE] [PRP] [NOUNSINGLE] [PRP] NOUN<type:C> PRP<lemma:$PrepRA> NOUNSINGLE|PRO<type:D|P|I|X>
%
CprepR: [NOUNSINGLE] [PRP] [NOUNSINGLE] [PRP] [NOUNSINGLE] [PRP] NOUN<type:C> PRP<lemma:$PrepRA> NOUNSINGLE|PRO<type:D|P|I|X>
%
CprepR: [NOUNSINGLE] [PRP] [NOUNSINGLE] [PRP] NOUN<type:C> PRP<lemma:$PrepRA> NOUNSINGLE|PRO<type:D|P|I|X>
%
CprepR: [NOUNSINGLE] [PRP] NOUN<type:C> PRP<lemma:$PrepRA> NOUNSINGLE|PRO<type:D|P|I|X>
%
CprepR: NOUN<type:C> PRP<lemma:$PrepRA> NOUNSINGLE|PRO<type:D|P|I|X>
%

###Depois só com "de" para todos os casos
CprepR: [NOUNSINGLE] [PRP] [NOUNSINGLE] [PRP] [NOUNSINGLE] [PRP] [NOUNSINGLE] [PRP] NOUNSINGLE PRP<lemma:de> NOUNSINGLE|PRO<type:D|P|I|X>
%
CprepR: [NOUNSINGLE] [PRP] [NOUNSINGLE] [PRP] [NOUNSINGLE] [PRP] NOUNSINGLE PRP<lemma:de> NOUNSINGLE|PRO<type:D|P|I|X>
%
CprepR: [NOUNSINGLE] [PRP] [NOUNSINGLE] [PRP] NOUNSINGLE PRP<lemma:de> NOUNSINGLE|PRO<type:D|P|I|X>
%
CprepR: [NOUNSINGLE] [PRP] NOUNSINGLE PRP<lemma:de> NOUNSINGLE|PRO<type:D|P|I|X>
%
CprepR: NOUNSINGLE PRP<lemma:de> NOUNSINGLE|PRO<type:D|P|I|X>
%

##uno/este/otro de Valencia
CprepR: X<lemma:uno>|PRO<type:[DI]> PRP NOUNSINGLE|PRO<type:D|P|I|X>
Add: tag:PRO
%

#TermR: PRP<lemma:de&nomin:yes> NOUNSINGLE|PRO<type:D|P|I|X>
TermR: PRP<nomin:yes> NOUNSINGLE|PRO<type:D|P|I|X>
%


########coordinaçao NOUN

CoordL: NP [Fc] [NP] CONJ<(type:C)|(lemma:$CCord)> [NP]
NEXT
PunctL: [NP] Fc [NP] CONJ<(type:C)|(lemma:$CCord)> [NP]
Recursivity: 10
%


CoordL: [Fc]? NP CONJ<(type:C)|(lemma:$CCord)> [NP]
NEXT
CoordR: [Fc]? [NP] CONJ<(type:C)|(lemma:$CCord)> NP
Add: coord:noun
#Inherit: gender, number
%

PunctL: Fc CONJ<coord:noun>
%

#######Aposicao-1 com virgulas ou paranteses 

PunctL: [NOUNCOORD|PRO<type:D|P|I|X>] Fc|Fpa|Fca NOUNCOORD|PRO<type:D|P|I|X>|CARD [Fc|Fpt|Fct]
NEXT
PunctR: [NOUNCOORD|PRO<type:D|P|I|X>] [Fc|Fpa|Fca] NOUNCOORD|PRO<type:D|P|I|X>|CARD Fc|Fpt|Fct
NEXT
AdjnR: NOUNCOORD|PRO<type:D|P|I|X> [Fc|Fpa|Fca] NOUNCOORD|PRO<type:D|P|I|X>|CARD [Fc|Fpt|Fct]
%

PunctL: [NOUNCOORD|PRO<type:D|P|I|X>] Fc|Fpa|Fca [PRP] NOUNCOORD|PRO<type:D|P|I|X>|CARD [Fc|Fpt|Fct]
NEXT
PunctR: [NOUNCOORD|PRO<type:D|P|I|X>] [Fc|Fpa|Fca] [PRP] NOUNCOORD|PRO<type:D|P|I|X>|CARD Fc|Fpt|Fct
NEXT
CprepR: NOUNCOORD|PRO<type:D|P|I|X> [Fc|Fpa|Fca] PRP NOUNCOORD|PRO<type:D|P|I|X>|CARD [Fc|Fpt|Fct]
%


AdjnR: NOUNCOORD|PRO<type:D|P|I|X> [Fc|Fpa|Fca] VERB<mode:P> [X]? [X]? [X]? [X]? [X]? [X]? [X]? [X]? [X]? [X]? [Fc|Fpt|Fct] 
NoUniq
%


##################Fim Frase Preposicionais de nomes#################




##erros antes de relativas: 'que' etiquetado como PRO com nomes que subcategorizam conjunçao 'que'
Single: [VERB<lemma:$SubcatClaus>]  [PRP<lemma:a>] [NOUNCOORD|PRO<type:D|P|I|X>] PRO<type:R>  [NOUNCOORD|PRO<type:D|P|I|X>]?  [VERB<mode:[^PNG]>]
Corr: tag:CONJ, type:S
%
Single: [VERB<lemma:$SubcatClaus>]  [ADJ] PRO<type:R>  [NOUNCOORD|PRO<type:D|P|I|X>]?  [VERB<mode:[^PNG]>]
Corr: tag:CONJ, type:S
%

###RELATIVAS 1
SubjL: [NOUN] PRO<type:R|W> VERB|CONJ<coord:verb>
NEXT
AdjnR: NOUN [PRO<type:R|W>] VERB|CONJ<coord:verb>
NoUniq
%
DobjL: [NOUN] PRO<type:R|W> [NOUNCOORD|PRO<type:D|P|I|X>] VERB|CONJ<coord:verb>
NEXT
AdjnR: NOUN [PRO<type:R|W>] [NOUNCOORD|PRO<type:D|P|I|X>] VERB|CONJ<coord:verb>
NoUniq
%
CircL: [NOUN|PRO<type:D|P|I|X>]  PRP PRO<type:R|W> VERB|CONJ<coord:verb>
NEXT
AdjnR: NOUN|PRO<type:D|P|I|X> [PRP] [PRO<type:R|W>] VERB|CONJ<coord:verb> 
NoUniq
%
AdjnR: NOUN|PRO<type:D|P|I|X>  VERB<mode:[GP]>|CONJ<coord:verb>
NoUniq
%



##################Frase CIRC especificas#################

#CircR: VERB<lemma:$PTa> [NOUNCOORD|PRO<type:D|P|I|X>] PRP<lemma:a> NOUNCOORD|PRO<type:D|P|I|X>|VERB<mode:N>|ADV
#%
#CircR: VERB<mode:[^P]> [NOUNCOORD|PRO<type:D|P|I|X>] PRP<lemma:$PrepMA> NOUNCOORD|PRO<type:D|P|I|X>|ADV
#%
CircR: VERB<mode:P> [NOUNCOORD|PRO<type:D|P|I|X>] PRP<lemma:por|by> NOUNCOORD|PRO<type:D|P|I|X>|ADV
%
PunctR: VERB Fc [NOUNCOORD|PRO<type:D|P|I|X>] [PRP<lemma:$PrepMA>] [CARD|DATE]
NEXT
CircR: VERB [Fc]? [NOUNCOORD|PRO<type:D|P|I|X>] PRP<lemma:$PrepMA> CARD|DATE
%
##################Fim CIRC#################


############Frases Cprep 2 (casos especiais)

CprepR: CARD PRP<lemma:de|entre|sobre|of|about|between> NOUNCOORD|PRO
%

CprepR: PRO<type:P> PRP<lemma:de|of> NOUNCOORD|PRO
%


CprepR: NOUNCOORD [PRP] [PRO<type:D|P|I|X>] PRP NOUNCOORD|ADV
NEXT
CprepR: NOUNCOORD PRP PRO<type:D|P|I|X> [PRP] [NOUNCOORD|ADV]
%



##########Caso especial de coordinaçao: 'industria da Galiza e do Portugal', 'venho de Madrid e de Barcelona' 
##hai que associar o nome de relação com CprepR e CircR!!


PrepR: VERB [NOUNCOORD|PRO<type:D|P|I|X>] CONJ<coord:cprep>
%

PrepR: NOUNCOORD|PRO<type:D|P|I|X> CONJ<coord:cprep>
%

##############



#############sustantivas

##que como sujeito

SubjL: PRO<sust:yes>  VERB
Add: nomin:yes
Inherit: number, person
%

##que como objeto direito

SubjL: [PRO<sust:yes>] NOUNCOORD|PRO<type:D|P|I|X> VERB
NEXT
DobjL: PRO<sust:yes> [NOUNCOORD|PRO<type:D|P|I|X>] VERB
Add: nomin:yes
Inherit: number, person
%

##########fim sustantivas


####Sujeito a direita do verbo (SV)
SubjR: VERB<lemma:$VS> NOUNCOORD|PRO<type:D|P|I|X>
Agr: number, person
%

##com pasiva reflea
SubjR: VERB<se:yes&lemma:$VSrefleja> NOUNCOORD|PRO<type:D|P|I|X>
Agr: number, person
%
##com pronome indirecto
SubjR: VERB<ind:yes&lemma:$VSind> NOUNCOORD|PRO<type:D|P|I|X>
Agr: number, person
%



##objecto directo com preposição a:
DobjPrepR: VERB<lemma:$SubcatTr> PRP<lemma:a> NOUNCOORD|PRO<type:D|P|I|X>
%


###########Atributivas (so adjectivais)

AtrR: VERB<lemma:ser> NOUNCOORD|PRO<type:D|P|I|X>
%
AtrR: VERB ADJ|CONJ<coord:adj>
%


############# OBJ DIRECTO (so nominal)
DobjR: VERB NOUNCOORD|PRO<type:D|P|I|X>
%




###Complementos Preposicionais argumentais (Creg)

IobjR: VERB<lemma:($SubcatIND)|(SubcatBitr)>  [NOUNCOORD|PRO<type:D|P|I|X>]? PRP<lemma:a> NOUNCOORD|PRO<type:D|P|I|X>|VERB<mode:N>
%

CregR: VERB<lemma:$SubcatA>  [NOUNCOORD|PRO<type:D|P|I|X>]? PRP<lemma:a> NOUNCOORD|PRO<type:D|P|I|X>|VERB<mode:N>
%
CregR: VERB<lemma:$SubcatEN>  [NOUNCOORD|PRO<type:D|P|I|X>]? PRP<lemma:en> NOUNCOORD|PRO<type:D|P|I|X>|VERB<mode:N>
%
CregR: VERB<lemma:$SubcatDE>  [NOUNCOORD|PRO<type:D|P|I|X>]? PRP<lemma:de> NOUNCOORD|PRO<type:D|P|I|X>|VERB<mode:N>
%
CregR: VERB<lemma:$SubcatCON>  [NOUNCOORD|PRO<type:D|P|I|X>]? PRP<lemma:con> NOUNCOORD|PRO<type:D|P|I|X>|VERB<mode:N>
%
CregR: VERB<lemma:$SubcatPOR>  [NOUNCOORD|PRO<type:D|P|I|X>]? PRP<lemma:por> NOUNCOORD|PRO<type:D|P|I|X>|VERB<mode:N>
%
CregR: VERB<lemma:$SubcatHACIA>  [NOUNCOORD|PRO<type:D|P|I|X>]? PRP<lemma:hacia> NOUNCOORD|PRO<type:D|P|I|X>|VERB<mode:N>
%
CregR: VERB<lemma:$SubcatSOBRE>  [NOUNCOORD|PRO<type:D|P|I|X>]? PRP<lemma:sobre> NOUNCOORD|PRO<type:D|P|I|X>|VERB<mode:N>
%


########coordinaçao NOUN (segunda fase)

CoordL: NP [Fc] [NP] CONJ<(type:C)|(lemma:$CCord)> [NP]
NEXT
PunctL: [NP] Fc [NP] CONJ<(type:C)|(lemma:$CCord)> [NP]
Recursivity: 5
%

CoordL: [Fc]? NP CONJ<(type:C)|(lemma:$CCord)> [NP]
NEXT
CoordR: [Fc]? [NP] CONJ<(type:C)|(lemma:$CCord)> NP
Add: coord:noun
#Inherit: gender, number
%

PunctL: Fc CONJ<coord:noun>
%
#############################################





######COORDINAÇAO VERBOS (antes dos circunstancias)###############
##problemas com o Inherit: conj nao tem os atributos 'mode' e 'tense' -> so se herdam os valores

CoordL: VERB<nomin:no> [Fc] [VERB<nomin:no>] CONJ<(type:C)|(lemma:$CCord)> [VERB<nomin:no>]
NEXT
PunctL: [VERB<nomin:no>] Fc [VERB<nomin:no>] CONJ<(type:C)|(lemma:$CCord)> [VERB<nomin:no>]
Recursivity: 5
%

CoordL: [Fc]? VERB<nomin:no> CONJ<(type:C)|(lemma:$CCord)> [VERB<nomin:no>]
NEXT
CoordR: [Fc]? [VERB<nomin:no>] CONJ<(type:C)|(lemma:$CCord)> VERB<nomin:no>
Add: coord:verb
Inherit: mode, tense
%

PunctL: Fc CONJ<coord:verb>
%


############# CIRCUNSTANCIAIS

##comer peixe para estar no parque em Madrid
CircR: [VERB|CONJ<coord:verb>] [PRP] VERB|CONJ<coord:verb> [PRP] [CARD|NOUNCOORD|PRO<type:D|P|I|X>] PRP CARD|NOUNCOORD|PRO<type:D|P|I|X>
Recursivity: 1
%
CircR: [VERB|CONJ<coord:verb>] [PRP] VERB|CONJ<coord:verb> PRP CARD|NOUNCOORD|PRO<type:D|P|I|X>
NEXT
CircR: VERB|CONJ<coord:verb> PRP VERB|CONJ<coord:verb> [PRP] [CARD|NOUNCOORD|PRO<type:D|P|I|X>]
%

CircR: VERB|CONJ<coord:verb>  PRP VERB<mode:[^PG]>|ADV|CARD
Recursivity: 1
%

##observou ao olhar na casa
SpecL: [VERB|CONJ<coord:verb>]  [PRP] DET<type:A>  VERB<mode:N>|ADV|CARD
NEXT
CircR: VERB|CONJ<coord:verb>  PRP  [DET<type:A>] VERB<mode:N>|ADV|CARD
Recursivity: 1
%

CircR: VERB|CONJ<coord:verb> PRP NOUNCOORD|PRO<type:D|P|I|X>
Recursivity: 1
%


#CprepR: VERB CONJ<coord:cprep>
#%
##circunstanciais entre vírgulas
PunctR: VERB Fc [PRP] [NOUNCOORD|PRO<type:D|P|I|X>] [Fc]?
NEXT
PunctR: VERB [Fc] [PRP] [NOUNCOORD|PRO<type:D|P|I|X>] Fc
NEXT
TermR: [VERB] [Fc] PRP NOUNCOORD|PRO<type:D|P|I|X> [Fc]?
NEXT
CircR: VERB [Fc] PRP [NOUNCOORD|PRO<type:D|P|I|X>] [Fc]?
Recursivity:2
%

##circunstanciais a esquerda
PunctL: [PRP<pos:0>] [NOUNCOORD|PRO<type:D|P|I|X>] Fc  VERB<mode:[^PNG]>|CONJ<coord:verb>
NEXT
CircL: PRP<pos:0> NOUNCOORD|PRO<type:D|P|I|X> [Fc]?  VERB<mode:[^PNG]>|CONJ<coord:verb>
%


PunctL: Fc [PRP] [NOUNCOORD|PRO<type:D|P|I|X> ] [Fc]?  VERB<mode:[^PNG]>|CONJ<coord:verb>
NEXT
PunctL: [Fc]? [PRP] [NOUNCOORD|PRO<type:D|P|I|X> ] Fc  VERB<mode:[^PNG]>|CONJ<coord:verb>
NEXT
CircL: [Fc]? PRP NOUNCOORD|PRO<type:D|P|I|X>  [Fc]?  VERB<mode:[^PNG]>|CONJ<coord:verb>
Recursivity:1
%



###########COMPLEMENTOS VERBAIS com nominais e gerundios (atributivas, circunstaciais, objecto directo)


## "parece cansado" (V+VPP), hai que tratar os predicativos de verbos atributivos, ja que hai muitos adjectivos etiquetados como participios
AtrR: VERB<lemma:$SubcatAtr>  VERB<mode:[PNG]>
%

##si o gerundio aparece com um não atributivo, entao Circ
Circ2R: VERB VERB<mode:[GP]>
%

####Sujeito a direita do verbo (SV)
SubjR: VERB<lemma:$VS> VERB<mode:N>
Agr: number, person
%
##com pasiva refleja
SubjR: VERB<se:yes&lemma:$VSrefleja> VERB<mode:N>
Agr: number, person
%
##com pronome indirecto
SubjR: VERB<ind:yes&lemma:$VSind> VERB<mode:N>
Agr: number, person
%

#objecto verbal (quiero/suelo comer)
DobjR: VERB VERB<mode:N>
%



###TRATAMENTO DE DATAS
AdjnR:  VERB<mode:[^PNG]> DATE
%

PunctL: Fc [DATE] VERB<mode:[^PNG]>
NEXT
AdjnL:  [Fc]? DATE VERB<mode:[^PNG]>
%


##ultimos pp attachments nominais (que ficarom soltos)
CprepR: NOUNCOORD PRP NOUNCOORD
%


#completivas
##bloco: V CONJ<que> V

##erros: 'que' etiquetado como PRO com nomes que subcategorizam conjunçao 'que'
Single: [VERB<lemma:$SubcatClaus>]  PRO<type:R>  [NOUNCOORD|PRO<type:D|P|I|X>]?  [VERB<mode:[^PNG]>]
Corr: tag:CONJ, type:S
%

SpecL: [VERB] CONJ<lemma:que|si> VERB<mode:[^PNG]>
NEXT
DobjR: VERB  [CONJ<lemma:que|si>] VERB<mode:[^PNG]>
%

SpecL: [VERB]  CONJ<lemma:que|si>  [NOUNCOORD|PRO<type:D|P|I|X>] VERB<mode:[^PNG]>
NEXT
SubjL:  [VERB]  [CONJ<lemma:que|si>]  NOUNCOORD|PRO<type:D|P|I|X> VERB<mode:[^PNG]>
NEXT
DobjR: VERB   [CONJ<lemma:que|si>] [NOUNCOORD|PRO<type:D|P|I|X>] VERB<mode:[^PNG]>
%


SpecL: [VERB] [PRP] CONJ<lemma:que> VERB<mode:[^PNG]>
NEXT
CircR: VERB PRP [CONJ<lemma:que>] VERB<mode:[^PNG]>
%


#######aposicao-2 com virgulas ou paranteses

PunctL: [NOUNCOORD|PRO<type:D|P|I|X>] Fc|Fpa|Fca VERB [Fc|Fpt|Fct]
NEXT
PunctR: [NOUNCOORD|PRO<type:D|P|I|X>] [Fc|Fpa|Fca] VERB Fc|Fpt|Fct
NEXT
AdjnR: NOUNCOORD|PRO<type:D|P|I|X> [Fc|Fpa|Fca] VERB [Fc|Fpt|Fct]
%

PunctL: [NOUNCOORD|PRO<type:D|P|I|X>] Fc|Fpa|Fca [PRP] NOUNCOORD|PRO<type:D|P|I|X>|CARD [Fc|Fpt|Fct]
NEXT
PunctR: [NOUNCOORD|PRO<type:D|P|I|X>] [Fc|Fpa|Fca] [PRP] NOUNCOORD|PRO<type:D|P|I|X>|CARD Fc|Fpt|Fct
NEXT
CprepR: NOUNCOORD|PRO<type:D|P|I|X> [Fc|Fpa|Fca] PRP NOUNCOORD|PRO<type:D|P|I|X>|CARD [Fc|Fpt|Fct]
%


AdjnL: [Fc] VERB<mode:P> [Fc] VERB
NEXT
PunctL: Fc [VERB<mode:P>] [Fc] VERB
NEXT
PunctL: [Fc] [VERB<mode:P>] Fc VERB
%


########coordinaçao NOUN (tercera fase)

CoordL: NP [Fc] [NP] CONJ<(type:C)|(lemma:$CCord)> [NP]
NEXT
PunctL: [NP] Fc [NP] CONJ<(type:C)|(lemma:$CCord)> [NP]
Recursivity: 10
%

CoordL: [Fc]? NP CONJ<(type:C)|(lemma:$CCord)> [NP]
NEXT
CoordR: [Fc]? [NP] CONJ<(type:C)|(lemma:$CCord)> NP
Add: coord:noun
#Inherit: gender, number
%

PunctL: Fc CONJ<coord:noun>
%
#############################


######COORDINAÇAO VERBOS (antes do sujeito)###############
CoordL: VERB<nomin:no> [Fc] [VERB<nomin:no>] CONJ<(type:C)|(lemma:$CCord)> [VERB<nomin:no>]
NEXT
PunctL: [VERB<nomin:no>] Fc [VERB<nomin:no>] CONJ<(type:C)|(lemma:$CCord)> [VERB<nomin:no>]
Recursivity: 5
%


CoordL: [Fc]? VERB<nomin:no> CONJ<(type:C)|(lemma:$CCord)> [VERB<nomin:no>]
NEXT
CoordR: [Fc]? [VERB<nomin:no>] CONJ<(type:C)|(lemma:$CCord)> VERB<nomin:no>
Add: coord:verb
Inherit: mode, tense
%

PunctL: Fc CONJ<coord:verb>
%


###SUJEITOS
SubjL: NOUN<type:P> VERB<mode:[^PG]>|CONJ<coord:verb&mode:[^PG]>
Add: subj:yes
%

SubjL: NOMINAL|PRO<type:D|P|I|X> VERB<mode:[^PG]>|CONJ<coord:verb&mode:[^PG]>
#Agreement: person, number
Add: subj:yes
%


#############COMPLEMENTOS VERBAIS COM NOMINALIZAÇOES (NOMINAL)##############
####Sujeito a direita do verbo (SV)

SubjR: VERB<lemma:$VS> NOMINAL|PRO<type:D|P|I|X>
Agr: number, person
%
##com pasiva reflea
SubjR: VERB<se:yes&lemma:$VSrefleja> NOMINAL|PRO<type:D|P|I|X>
Agr: number, person
%
##com pronome indirecto
SubjR: VERB<ind:yes&lemma:$VSind> NOMINAL|PRO<type:D|P|I|X>
Agr: number, person
%


##objecto directo com preposição a:
DobjPrepR: VERB<lemma:$SubcatTr> PRP<lemma:a> NOMINAL|PRO<type:D|P|I|X>
%


###########Atributivas (so adjectivais)

AtrR: VERB<lemma:ser> NOMINAL|PRO<type:D|P|I|X>
%


############# OBJ DIRECTO (so nominal)
DobjR: VERB NOMINAL|PRO<type:D|P|I|X>
%

###Complementos Preposicionais argumentais (Creg)

IobjR: VERB<lemma:($SubcatIND)|(SubcatBitr)>  [NOUNCOORD|PRO<type:D|P|I|X>]? PRP<lemma:a> NOMINAL|PRO<type:D|P|I|X>|VERB<mode:N>
%

CregR: VERB<lemma:$SubcatA>  [NOMINAL|PRO<type:D|P|I|X>]? PRP<lemma:a> NOMINAL|PRO<type:D|P|I|X>|VERB<mode:N>
%
CregR: VERB<lemma:$SubcatEN>  [NOMINAL|PRO<type:D|P|I|X>]? PRP<lemma:en> NOMINAL|PRO<type:D|P|I|X>|VERB<mode:N>
%
CregR: VERB<lemma:$SubcatDE>  [NOMINAL|PRO<type:D|P|I|X>]? PRP<lemma:de> NOMINAL|PRO<type:D|P|I|X>|VERB<mode:N>
%
CregR: VERB<lemma:$SubcatCON>  [NOMINAL|PRO<type:D|P|I|X>]? PRP<lemma:con> NOMINAL|PRO<type:D|P|I|X>|VERB<mode:N>
%
CregR: VERB<lemma:$SubcatPOR>  [NOMINAL|PRO<type:D|P|I|X>]? PRP<lemma:por> NOMINAL|PRO<type:D|P|I|X>|VERB<mode:N>
%
CregR: VERB<lemma:$SubcatHACIA>  [NOMINAL|PRO<type:D|P|I|X>]? PRP<lemma:hacia> NOMINAL|PRO<type:D|P|I|X>|VERB<mode:N>
%
CregR: VERB<lemma:$SubcatSOBRE>  [NOMINAL|PRO<type:D|P|I|X>]? PRP<lemma:sobre> NOMINAL|PRO<type:D|P|I|X>|VERB<mode:N>
%

###############FIM #############COMPLEMENTOS VERBAIS COM NOMINALIZAÇOES (NOMINAL)#############################


######################RELATIVAS#########################


#############especificativas e explicativas de objecto direito
PunctL: [NOUN|PRO<type:D|P|I|X>] Fc [PRO<type:R|W>] VERB<subj:yes>|CONJ<subj:yes&coord:verb>    [Fc]?
NEXT
PunctR: [NOUN|PRO<type:D|P|I|X>] [Fc] [PRO<type:R|W>] VERB<subj:yes>|CONJ<subj:yes&coord:verb>   Fc
NEXT
DobjL: [NOUN|PRO<type:D|P|I|X>] [Fc]? PRO<type:R|W> VERB<subj:yes>|CONJ<subj:yes&coord:verb>    [Fc]?
NEXT
AdjnR: NOUN|PRO<type:D|P|I|X> [Fc]? [PRO<type:R|W>] VERB<subj:yes>|CONJ<subj:yes&coord:verb>    [Fc]?
%

#############especificativas e explicativas de sujeito
PunctL: [NOUN|PRO<type:D|P|I|X>] Fc [PRO<type:R|W>] VERB|CONJ<coord:verb>   [Fc]?
NEXT
PunctR: [NOUN|PRO<type:D|P|I|X>] [Fc]? [PRO<type:R|W>] VERB|CONJ<coord:verb> Fc
NEXT
SubjL: [NOUN|PRO<type:D|P|I|X>] [Fc]? PRO<type:R|W> VERB|CONJ<coord:verb>   [Fc]?
NEXT
AdjnR: NOUN|PRO<type:D|P|I|X> [Fc]? [PRO<type:R|W>] VERB|CONJ<coord:verb>  [Fc]?
%


#############especificativas e explicativas de ccirc

PunctL: [NOUN|PRO<type:D|P|I|X>] Fc [PRP] [PRO<type:R|W>] VERB|CONJ<coord:verb>   [Fc]?
NEXT
PunctR: [NOUN|PRO<type:D|P|I|X>] [Fc]?  [PRP] [PRO<type:R|W>] VERB|CONJ<coord:verb> Fc
NEXT
CircL: [NOUN|PRO<type:D|P|I|X>] [Fc]? PRP PRO<type:R|W> VERB|CONJ<coord:verb>  [Fc]?
NEXT
AdjnR: NOUN|PRO<type:D|P|I|X> [Fc]? [PRP] [PRO<type:R|W>] VERB|CONJ<coord:verb>  [Fc]?
%


##relativas com gerundio/participio (INGLES)
PunctL: [NOUNCOORD|PRO<type:D|P|I|X>] Fc VERB<mode:[GP]>|CONJ<coord:verb>
NEXT
AdjnR: NOUNCOORD|PRO<type:D|P|I|X> [Fc]? VERB<mode:[GP]>|CONJ<coord:verb>
%


######################FIM RELATIVAS#########################


#############NIVEL INTRACLAUSAS#############

########DEPENDENCIAS COM CONJUNÇOES SUBORDINADAS###############
AdjnR: VERB CONJ<type:S> [VERB]
NEXT
AdjnR: VERB [CONJ<type:S>] VERB
%

########FIM DEPENDENCIAS COM CONJUNÇOES SUBORDINADAS###############



#####prep+nom|verb isolado

TermR: PRP NOUNCOORD|PRO<type:D|P|I|X>|VERB
NoUniq
%
#####

